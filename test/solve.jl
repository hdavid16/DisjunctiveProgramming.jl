using HiGHS, Ipopt, Juniper, Pajarito, Hypatia
function test_linear_gdp_example(m, use_complements = false)
    set_attribute(m, MOI.Silent(), true)
    @variable(m, 1 ≤ x[1:2] ≤ 9)
    if use_complements
        @variable(m, Y1, Logical)
        @variable(m, Y2, Logical, logical_complement = Y1)
        Y = [Y1, Y2]
    else
        @variable(m, Y[1:2], Logical)
    end
    @variable(m, W[1:2], Logical)
    @objective(m, Max, sum(x))
    @constraint(m, y1[i=1:2], [1,4][i] ≤ x[i] ≤ [3,6][i], Disjunct(Y[1]))
    @constraint(m, w1[i=1:2], [1,5][i] ≤ x[i] ≤ [2,6][i], Disjunct(W[1]))
    @constraint(m, w2[i=1:2], [2,4][i] ≤ x[i] ≤ [3,5][i], Disjunct(W[2]))
    @constraint(m, y2[i=1:2], [8,1][i] ≤ x[i] ≤ [9,2][i], Disjunct(Y[2]))
    @disjunction(m, inner, [W[1], W[2]], Disjunct(Y[1]))
    @disjunction(m, outer, [Y[1], Y[2]])

    @test optimize!(m, gdp_method = BigM()) isa Nothing
    @test termination_status(m) == MOI.OPTIMAL
    @test objective_value(m) ≈ 11
    @test value.(x) ≈ [9,2]
    @test !value(Y[1])
    @test value(Y[2])
    @test !value(W[1])
    @test !value(W[2])
    @test optimize!(m, gdp_method = Hull()) isa Nothing
    @test termination_status(m) == MOI.OPTIMAL
    @test objective_value(m) ≈ 11
    @test value.(x) ≈ [9,2]
    @test !value(Y[1])
    @test value(Y[2])
    @test !value(W[1])
    @test !value(W[2])
    @test value(variable_by_name(m, "x[1]_W[1]")) ≈ 0
    @test value(variable_by_name(m, "x[1]_W[2]")) ≈ 0
    @test value(variable_by_name(m, "x[2]_W[1]")) ≈ 0
    @test value(variable_by_name(m, "x[2]_W[2]")) ≈ 0
    if !use_complements
        @test value(variable_by_name(m, "x[1]_Y[1]")) ≈ 0
        @test value(variable_by_name(m, "x[1]_Y[2]")) ≈ 9
        @test value(variable_by_name(m, "x[2]_Y[1]")) ≈ 0
        @test value(variable_by_name(m, "x[2]_Y[2]")) ≈ 2
    end

    @test optimize!(m, gdp_method = MBM(HiGHS.Optimizer)) isa Nothing
    @test termination_status(m) == MOI.OPTIMAL
    @test objective_value(m) ≈ 11
    @test value.(x) ≈ [9,2]
    @test !value(Y[1])
    @test value(Y[2])
    @test !value(W[1])
    @test !value(W[2])

    @test optimize!(m, gdp_method = CuttingPlanes(HiGHS.Optimizer)) isa Nothing
    @test termination_status(m) == MOI.OPTIMAL
    @test objective_value(m) ≈ 11 atol=1e-3
    @test value.(x) ≈ [9,2] atol=1e-3
    @test !value(Y[1])
    @test value(Y[2])
    @test !value(W[1])
    @test !value(W[2])
  

    m_copy, ref_map = JuMP.copy_model(m)
    lv_map = DP.copy_gdp_data(m, m_copy, ref_map)
    set_optimizer(m_copy, HiGHS.Optimizer)
    set_optimizer_attribute(m_copy, "output_flag", false)
    optimize!(m_copy, gdp_method = BigM())
    @test termination_status(m_copy) == MOI.OPTIMAL
    @test objective_value(m_copy) ≈ 11 atol=1e-3
    @test value.(x) ≈ value.(ref_map[x]) atol=1e-3
    @test value.(Y[1]) == value.(lv_map[Y[1]]) 
    @test value.(Y[2]) == value.(lv_map[Y[2]])
    @test !value(W[1])
    @test !value(W[2])

end

function test_quadratic_gdp_example(use_complements = false) #psplit does not work with complements
    ipopt = optimizer_with_attributes(Ipopt.Optimizer,"print_level"=>0,"sb"=>"yes")
    optimizer = optimizer_with_attributes(Juniper.Optimizer, "nl_solver"=>ipopt)
    m = GDPModel(optimizer)
    set_attribute(m, MOI.Silent(), true)
    @variable(m, 0 ≤ x[1:2] ≤ 10)
    
    if use_complements
        @variable(m, Y1, Logical)
        @variable(m, Y2, Logical, logical_complement = Y1)
        Y = [Y1, Y2]
    else
        @variable(m, Y[1:2], Logical)
    end
    @variable(m, W[1:2], Logical)
    
    @objective(m, Max, sum(x))
    
    @constraint(m, y1_quad, x[1]^2 + x[2]^2 ≤ 16, Disjunct(Y[1]))
    @constraint(m, w1[i=1:2], [1, 2][i] ≤ x[i] ≤ [3, 4][i], Disjunct(W[1]))
    @constraint(m, w1_quad, x[1]^2 ≥ 2, Disjunct(W[1]))
    
    @constraint(m, w2[i=1:2], [2, 1][i] ≤ x[i] ≤ [4, 3][i], Disjunct(W[2]))
    @constraint(m, w2_quad, x[1]^2 + x[2] ≤ 10, Disjunct(W[2]))
    
    @constraint(m, y2_quad, x[1]^2 + 2*x[2]^2 ≤ 25, Disjunct(Y[2]))
    @constraint(m, y2[i=1:2], [3, 2][i] ≤ x[i] ≤ [5, 3][i], Disjunct(Y[2]))
    
    @disjunction(m, inner, [W[1], W[2]], Disjunct(Y[1]))
    @disjunction(m, outer, [Y[1], Y[2]])
    
    @test optimize!(m, gdp_method = BigM()) isa Nothing
    @test termination_status(m) in [MOI.OPTIMAL, MOI.LOCALLY_SOLVED]
    @test objective_value(m) ≈ 6.1237 atol=1e-3  
    @test value.(x) ≈ [4.0825, 2.0412] atol=1e-3 
    @test !value(Y[1]) 
    @test value(Y[2])
    @test !value(W[1]) 
    @test !value(W[2])

    @test optimize!(m, gdp_method = MBM(optimizer)) isa Nothing
    @test termination_status(m) in [MOI.OPTIMAL, MOI.LOCALLY_SOLVED]
    @test objective_value(m) ≈ 6.1237 atol=1e-3  
    @test value.(x) ≈ [4.0825, 2.0412] atol=1e-3 
    @test !value(Y[1]) 
    @test value(Y[2])
    @test !value(W[1]) 
    @test !value(W[2])

    @test optimize!(m, gdp_method = PSplit(2,m)) isa Nothing
    @test termination_status(m) in [MOI.OPTIMAL, MOI.LOCALLY_SOLVED]
    @test objective_value(m) ≈ 6.1237 atol=1e-3  
    @test value.(x) ≈ [4.0825, 2.0412] atol=1e-3 
    @test !value(Y[1]) 
    @test value(Y[2])
    @test !value(W[1]) 
    @test !value(W[2])
end

function test_generic_model(m)
    set_attribute(m, MOI.Silent(), true)
    @variable(m, 1 ≤ x[1:2] ≤ 9)
    @variable(m, Y[1:2], Logical)
    @variable(m, W[1:2], Logical)
    @objective(m, Max, sum(x))
    @constraint(m, y1[i=1:2], [1,4][i] ≤ x[i] ≤ [3,6][i], Disjunct(Y[1]))
    @constraint(m, w1[i=1:2], [1,5][i] ≤ x[i] ≤ [2,6][i], Disjunct(W[1]))
    @constraint(m, w2[i=1:2], [2,4][i] ≤ x[i] ≤ [3,5][i], Disjunct(W[2]))
    @constraint(m, y2[i=1:2], [8,1][i] ≤ x[i] ≤ [9,2][i], Disjunct(Y[2]))
    @disjunction(m, inner, [W[1], W[2]], Disjunct(Y[1]))
    @disjunction(m, outer, [Y[1], Y[2]])

    @test optimize!(m, gdp_method = BigM()) isa Nothing
    @test optimize!(m, gdp_method = Hull()) isa Nothing
    @test optimize!(m, gdp_method = Indicator()) isa Nothing

    # TODO add meaningful tests to check the constraints/variables
end

@testset "Solve Linear GDP" begin
    test_linear_gdp_example(GDPModel(HiGHS.Optimizer))
    test_linear_gdp_example(GDPModel(HiGHS.Optimizer), true)
    mockoptimizer = () -> MOI.Utilities.MockOptimizer(
        MOI.Utilities.UniversalFallback(MOIU.Model{Float32}()),
        eval_objective_value = false
        )
    test_quadratic_gdp_example()
    test_generic_model(GDPModel{Float32}(mockoptimizer))
end

function test_conic_gdp_example()
    # Circle-containment idea from Bernal Neira & Grossmann (2021),
    # Eq. 4.3: the point (x, y) must lie in the unit circle around
    # (0, 0) OR around (5, 0), expressed as second-order cones. The
    # disjunction picks one circle; minimizing x selects the first and
    # lands on its leftmost point, (x, y) = (-1, 0).
    oa = optimizer_with_attributes(HiGHS.Optimizer, MOI.Silent() => true)
    cs = optimizer_with_attributes(Hypatia.Optimizer, MOI.Silent() => true)
    paj = optimizer_with_attributes(Pajarito.Optimizer,
        "oa_solver" => oa, "conic_solver" => cs, "verbose" => false)
    for meth in (BigM(100), Hull())
        m = GDPModel(paj)
        set_attribute(m, MOI.Silent(), true)
        @variable(m, -10 <= x <= 10)
        @variable(m, -10 <= y <= 10)
        @variable(m, Y[1:2], Logical)
        @objective(m, Min, x)
        @constraint(m, c1, [1, x, y] in SecondOrderCone(), Disjunct(Y[1]))
        @constraint(m, c2, [1, x - 5, y] in SecondOrderCone(), Disjunct(Y[2]))
        @disjunction(m, Y)
        @test optimize!(m, gdp_method = meth) isa Nothing
        @test termination_status(m) == MOI.OPTIMAL
        @test isapprox(objective_value(m), -1, atol = 1e-4)
        @test isapprox(value(x), -1, atol = 1e-4)
        @test isapprox(value(y), 0, atol = 1e-4)
        @test value(Y[1])
        @test !value(Y[2])
    end
end

@testset "Solve Conic GDP" begin
    test_conic_gdp_example()
end

function test_exact_quadratic_gdp_example()
    # Quadratic version of the circle disjunction above: the point must
    # lie in the unit disk around (0, 0) or around (5, 0); minimizing x
    # selects the first disk at (x, y) = (-1, 0). The exact hull
    # reformulations (CEHR/GEHR, Gusev & Bernal Neira 2025) must agree
    # with BigM and the ε-approximated hull.
    ipopt = optimizer_with_attributes(Ipopt.Optimizer,
        "print_level" => 0, "sb" => "yes")
    juniper = optimizer_with_attributes(Juniper.Optimizer,
        "nl_solver" => ipopt)
    for meth in (BigM(100), Hull(), Hull(quadratic = :exact),
                 Hull(quadratic = :gehr))
        m = GDPModel(juniper)
        set_attribute(m, MOI.Silent(), true)
        @variable(m, -10 <= x <= 10)
        @variable(m, -10 <= y <= 10)
        @variable(m, Y[1:2], Logical)
        @objective(m, Min, x)
        @constraint(m, x^2 + y^2 <= 1, Disjunct(Y[1]))
        @constraint(m, (x - 5)^2 + y^2 <= 1, Disjunct(Y[2]))
        @disjunction(m, Y)
        @test optimize!(m, gdp_method = meth) isa Nothing
        @test termination_status(m) in (MOI.OPTIMAL, MOI.LOCALLY_SOLVED)
        @test isapprox(objective_value(m), -1, atol = 1e-4)
        @test isapprox(value(x), -1, atol = 1e-4)
        @test value(Y[1])
        @test !value(Y[2])
    end
end

function test_cehr_conic_gdp_example()
    # Same disks as above, but reformulated to explicit rotated SOCs
    # (quadratic = :cehr_conic) and solved as a true MICP.
    oa = optimizer_with_attributes(HiGHS.Optimizer, MOI.Silent() => true)
    cs = optimizer_with_attributes(Hypatia.Optimizer, MOI.Silent() => true)
    paj = optimizer_with_attributes(Pajarito.Optimizer,
        "oa_solver" => oa, "conic_solver" => cs, "verbose" => false)
    m = GDPModel(paj)
    set_attribute(m, MOI.Silent(), true)
    @variable(m, -10 <= x <= 10)
    @variable(m, -10 <= y <= 10)
    @variable(m, Y[1:2], Logical)
    @objective(m, Min, x)
    @constraint(m, x^2 + y^2 <= 1, Disjunct(Y[1]))
    @constraint(m, (x - 5)^2 + y^2 <= 1, Disjunct(Y[2]))
    @disjunction(m, Y)
    @test optimize!(m, gdp_method = Hull(quadratic = :cehr_conic)) isa Nothing
    @test termination_status(m) == MOI.OPTIMAL
    @test isapprox(objective_value(m), -1, atol = 1e-4)
    @test isapprox(value(x), -1, atol = 1e-4)
    @test value(Y[1])
    @test !value(Y[2])
end

@testset "Solve Exact Quadratic GDP" begin
    test_exact_quadratic_gdp_example()
    test_cehr_conic_gdp_example()
end
